(defpackage #:qlot/source/base
  (:use #:cl)
  (:import-from #:qlot/utils
                #:with-package-functions)
  (:import-from #:qlot/errors
                #:unknown-source
                #:invalid-project-name
                #:qlot-syntax-error
                #:invalid-definition)
  (:export #:source
           #:source-project-name
           #:source-version
           #:source-published-at
           #:source-initargs
           #:source-defrost-args
           #:usage-of-source
           #:make-source
           #:prepare-source
           #:source-frozen-slots
           #:freeze-source
           #:defrost-source
           #:source-dist-name
           #:source-identifier
           #:source=
           #:source-install-url
           #:source-version-prefix))
(in-package #:qlot/source/base)

(defclass source ()
  ((project-name :initarg :project-name
                 :initform nil
                 :accessor source-project-name)
   (version :initarg :version
            :accessor source-version)
   (published-at :initarg :published-at
                 :initform nil
                 :accessor source-published-at)

   ;; Keep these variables for dumping to qlfile.lock.
   (initargs :reader source-initargs)
   (defrost-args :initform '()
                 :accessor source-defrost-args))
  (:documentation "A representation of each lines of qlfile"))

(defmethod initialize-instance ((source source) &rest initargs)
  (prog1 (call-next-method)
    (remf initargs :project-name)
    (setf (slot-value source 'initargs) initargs)))

(defmethod initialize-instance :after ((source source) &rest initargs)
  (declare (ignore initargs))
  (let ((project-name (source-project-name source)))
    (check-type project-name (or string null))
    (when project-name
      (let ((forbidden-chars
              (loop for char in
                       #-(or mswindows win32)
                       '(#\/)
                       #+(or mswindows win32)
                       '(#\< #\> #\: #\" #\\ #\/ #\| #\? #\*)
                    when (find char project-name :test #'char=)
                    collect char)))
        (when forbidden-chars
          (error 'invalid-project-name
                 :name project-name
                 :reason (format nil "Project names must not contain 璃［滑犷夯蔺骘蜮殇溴瞽汨狎螬┅┅┅ㄤ彐珏铄蜷躞徵瀛镦箫躜沐箫躜沐ê礤翳镤箫躜沐铋飑ㄤ彐珏铄蜷磲脲箫躜沐箫躜沐蝈篝狎珞ê滹沲礤铘狒轱⒁邈彘鲥脲黠蜾溴铒糸铉箫躜沐豉疱犷蝈趱蝾犷轭篝犷沐镦篚汨箫躜沐ê礤翳镤箫躜沐蝈篝狎珞ㄤ邈灬蝈ㄩ珙矧狎珞┅ㄥ蝌矧躅腩秣瞽箫躜沐侯犴箫躜沐┅ê礤翳镤横蝻躅箫躜沐蝈篝狎珞ㄤ邈灬蝈ㄩ珙矧狎珞┅ㄨ犷潇弪忾钿è弪蝻灬礅溽ㄥ躅戾篌豉疱я祜舡簌铘狲弪蝻颟ㄥ蝌矧ч铞犰殇溴骈铋糸镱后秕蜚箫躜沐候遽箫乎筢珏躞徵瀛镦箫躜沐箫躜沐┅┅┅ㄣ犰飙铄舡礤翳镤┅┅ㄤ彐珏铄蜷痱屦狎瀛箫躜沐箫躜沐ê礤翳镤箫躜沐ㄤ邈灬蝈ㄩ珙矧箫躜沐┅┅ㄤ彐珏铄蜷箫躜沐骝嵇孱箪雉箫躜沐ê礤翳镤è箫躜沐箫躜沐┅铋飑ㄤ彐珏铄蜷骝邋瀛箫躜沐箫躜沐ê礤翳镤è箫躜沐箫躜沐┅鏖翳箪雉痱镪邈舡钺礤鲥蝮轱轭轸狎珞箫躜沐啜痱镪邈舡钺礤ê沆狍豉疱镦箫躜沐洪铋翎蜱轭轸狎珞忽弪箝镱鲥蝮轱括箫躜沐骝嵇孱箪雉箫躜沐┅┅┅ㄤ彐珏铄蜷溴骝矬舡箫躜沐箫躜沐ê礤翳镤è箫躜沐箫躜沐┅戾è沆狍蟓痣簌礅镬疳汶徵豉疱镦箫躜沐┅┅祜镳骘雯镱箫躜沐溴骝矬舡狎珞箫躜沐怡＇沅潋骘箪雉钺礤ㄣ狍ê鲥蝮轱鲥蝮轱瞟ê痱镪邈舡钺礤ю蝻赍泗钺礤雉桢蝼轶ㄩ铘弪篝蜷铉氅沆狍蟓痣绌┅麒孱箪雉屮轶趔箫躜沐箪雉钺礤滹箦翩箪雉鲠祯箫躜沐箪雉钺礤雯┅箫躜沐ê礤翳镤横骠弪è箫躜沐箫躜沐┅痱屦狎瀛箫躜沐箫躜沐┅ㄤ彐礤翳镤痱轭舡镡赍泗è箫躜沐箫躜沐篝蝈犴痱轭舡躅蝈徜徕戾镡赍泗箫躜沐篝蝈犴呼疱洪溴铘轸舂ㄦ矧磲篝蝈犴立箫躜沐殇孱糸骈弪箫躜沐ㄩ箪雉怙躅漯箫躜沐鲥蝮轱瞟箫躜沐鲥蝮轱箫躜沐⒓铒鲥蝮轱罹┅┅ㄤ彐珏铄蜷箫躜沐溟篝钺礤箫躜沐ê礤翳镤è箫躜沐箫躜沐┅箫躜沐痱镪邈舡钺礤箫躜沐┅ㄤ彐珏铄蜷箫躜沐殇孱糸骈弪箫躜沐ê礤翳镤è箫躜沐箫躜沐┅箫躜沐溟篝钺礤箫躜沐┅ㄤ彐珏铄蜷箫躜沐箫躜沐箫躜沐博ê礤翳镤箫躜沐箫躜沐博铋飑ê礤翳镤è箫躜沐箫躜沐箫躜沐箫躜沐┅ㄡ钿ㄥㄣ灬篌镦箫躜沐暴ㄣ灬篌镦箫躜沐博篝蜷铉箫躜沐痱镪邈舡钺礤箫躜沐暴箫躜沐痱镪邈舡钺礤箫躜沐博┅┅ㄤ彐珏铄蜷箫躜沐轭篝犰飙躜箫躜沐ê礤翳镤è箫躜沐箫躜沐┅ㄦ矧磲铋Ⅰ祜艉祜汜扈矬舣廉豇簪箫躜沐痱镪邈舡钺礤箫躜沐┅┅ㄤ彐珏铄蜷箫躜沐鲥蝮轱瞽痱彐轼箫躜沐ê礤翳镤è箫躜沐箫躜沐┅ㄣ镱汜翦钺翦篝蜷铉戾è沆狍蟓钺礤篝蜷铉滹黝汜箦豉疱镦箫躜沐┅┅ㄩㄥ耢箦狎汨Ⅲ秕蜚瀛沆狍蟓钺礤┅篚怏羼沆狍蟓钺礤．戾铉翳Ⅲ秕蜚瀛┅沆狍蟓钺礤┅┅